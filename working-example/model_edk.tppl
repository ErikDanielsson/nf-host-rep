import "stdlib::seq.mc"
import "lib.mc"
import "uniformization_branch_sampling.tppl"

// TreeLabeled is like the built in Tree type, but with leaf and node labels
type TreeLabeled =
  | Leaf{age: Real, label: Int}
  | Node{age: Real, left: TreeLabeled, right: TreeLabeled, label: Int}

  
type MsgTree =
  | MsgLeaf{
    age: Real, label: Int, outMsg: Matrix[Real]
  }
  | MsgNode{
    age: Real, label: Int,
    left: MsgTree, right: MsgTree,
    leftInMsg: Matrix[Real], rightInMsg: Matrix[Real],
    leftKernel: Matrix[Real], rightKernel: Matrix[Real],
    outMsg: Matrix[Real]
  }

type MarginalTree =
  | MarginalLeaf {
    age: Real, label: Int, marginalProb: Matrix[Real]
  }
  | MarginalNode {
    age: Real, label: Int,
    left: MarginalTree, right: MarginalTree,
    marginalProb: Matrix[Real]
  }

type NodeRepTree =
  | NodeRepLeaf{age: Real, label: Int, repertoire: Int[]}
  | NodeRepNode{age: Real, left: NodeRepTree, right: NodeRepTree, label: Int, repertoire: Int[]}

type HistoryPoint = HistoryPoint {switchTime: Real, fromState: Int, toState: Int, host: Int}

type HistoryTree =
  | HistoryLeaf {age: Real, label: Int, repertoire: Int[], history: HistoryPoint[]}
  | HistoryNode {age: Real, left: HistoryTree, right: HistoryTree, label: Int, repertoire: Int[], history: HistoryPoint[]}

type EmbeddedMarkovChainMatrix = EmbeddedMarkovChainMatrix { totalRates: Real[], transitionProbs: Real[][]}


type ReturnType = ReturnType{lambda: Real[], mu: Real, beta: Real, tree: NodeRepTree}

model function hostRepertoire(
  tree: TreeLabeled,
  nhosts: Int,
  interactions: Int[][]
): ReturnType {
  assume lambda ~ Dirichlet([1., 1., 1., 1.]) drift Dirichlet(lambda);
  assume mu ~ Exponential(10.) drift Exponential(10.);
  assume beta ~ Exponential(10.) drift Exponential(10.);

  let r = mtxCreate(3,3,
    [0.-lambda[1],                lambda[1],          0.0,
        lambda[2], 0.-(lambda[2]+lambda[3]),    lambda[3],
              0.0,                lambda[4], 0.-lambda[4]]
  );
  let qMatrix = mtxSclrMul(mu, r);

  let rootPrior = mtxCreate(nhosts, 3, ones(3 * nhosts));

  let postorderTree = postorderTraverse(tree, qMatrix, interactions, nhosts);

  // Sample the root
  let initRootRep = suggestRepAligned(postorderTree.outMsg, 1, nhosts);
  let rootRep = suggestRepRS(postorderTree.outMsg, nhosts, initRootRep);

  // Compute messages to pass to pass to the children of the root
  let newMsg = mtxCreate(nhosts, 3, observationMessage2(rootRep, 1, nhosts));
  let leftMsg = mtxMul(newMsg, postorderTree.leftKernel);
  let rightMsg = mtxMul(newMsg, postorderTree.rightKernel);

  // Construct an object containing the rate matrix in a suitable format for sampling 
  // from the embedded Markov chain
  let rootAge = postorderTree.age;
  // let embeddedQMatrix = rateMatrixToEmbeddedMarkovChain(qMatrix);
  let uParams = qMatrixtoUMatrix(qMatrix);
  let leftRepertoireTree = sampleNodeRepertoires(postorderTree.left, nhosts, leftMsg, rootRep, rootAge, uParams);
  let rightRepertoireTree = sampleNodeRepertoires(postorderTree.right, nhosts, rightMsg, rootRep, rootAge, uParams);

  // Construct the root node of the repertoire tree
  let repertoireTree = NodeRepNode {
    age = tree.age, label = tree.label,
    left = leftRepertoireTree, right = rightRepertoireTree,
    repertoire = rootRep
  };

  return ReturnType {
    lambda = lambda,
    mu = mu,
    beta = beta,
    tree = repertoireTree
  };
} 

// Compute postorder messages on the observed tree
function postorderTraverse(
  tree: TreeLabeled, qMatrix: Matrix[Real], interactions: Int[][], nhosts: Int
): MsgTree {
  if tree is Leaf {
    let outmsg = mtxCreate(nhosts, 3, observationMessage2(interactions[tree.label], 1, nhosts));
    // printLn("Postorder leaf");
    // printSeqInt(interactions[tree.label]);
    // printMtx(outmsg);
    return MsgLeaf{
      age = 0.0,
      label = tree.label,
      outMsg = mtxCreate(nhosts, 3, observationMessage2(interactions[tree.label], 1, nhosts))
    };
  }

  let left = postorderTraverse(tree.left, qMatrix, interactions, nhosts);
  let right = postorderTraverse(tree.right, qMatrix, interactions, nhosts);

    // These will be used when we propagate messages in the forward pass later
  let leftKernel = mtxExp(mtxSclrMul(tree.age-left.age, qMatrix));
  let rightKernel = mtxExp(mtxSclrMul(tree.age-right.age, qMatrix));

  // We are doing the backward pass, so we should transpose the kernels
  let leftBackwardKernel = mtxTrans(leftKernel);
  let rightBackwardKernel = mtxTrans(rightKernel);

  let leftInMsg = mtxMul(left.outMsg, leftBackwardKernel);
  let rightInMsg = mtxMul(right.outMsg, rightBackwardKernel);

  let outMsg = mtxElemMul(leftInMsg, rightInMsg);

  return MsgNode{
    age = tree.age, label = tree.label,
    left = left, right = right, 
    leftInMsg = leftInMsg, rightInMsg = rightInMsg,
    leftKernel = leftKernel, rightKernel = rightKernel, 
    outMsg = outMsg
  };
}

function suggestRepAligned(msg: Matrix[Real], i: Int, max: Int) {
  if i <= max {
    let param = mtx3ToSeq(msg, i);
    // printLn("Aligned");
    // printSeq(param, 1, 3);
    assume x ~ Categorical(param) drift Categorical(param); 
    return cons(x, suggestRepAligned(msg, i + 1, max));
  } else {
    return [];
  }
}

function suggestRepRS(msg: Matrix[Real], max: Int, initialRep: Int[]) {
  if any(is2, initialRep) {
    return initialRep;
  } else {
    let newRep = suggestRepUnaligned(msg, 1, max);
    return suggestRepRS(msg, max, newRep);
  }
}

function suggestRepUnaligned(msg: Matrix[Real], i: Int, max: Int) {
  if i <= max {
    let param = mtx3ToSeq(msg, i);
    // printLn("Unaligned");
    // printSeq(param, 1, 3);
    assume x ~ Categorical(param) drift Categorical(param); 
    return join([[x], suggestRepUnaligned(msg, i + 1, max)]);
  } else {
    return [];
  }
}

function sampleNodeRepertoires(
  tree: MsgTree,
  nhosts: Int,
  preorderMsg: Matrix[Real],
  parentRep: Int[],
  parentAge: Real,
  uParams: UniformizationParams
): NodeRepTree {
  if tree is MsgLeaf {
    let samplingProb = mtxElemMul(tree.outMsg, preorderMsg);
    // printLn("Leaf");
    // printMtx(samplingProb);
    let initRep = suggestRepAligned(samplingProb, 1, nhosts);
    let rep = suggestRepRS(samplingProb, nhosts, initRep);
    return NodeRepLeaf { age = tree.age, label = tree.label, repertoire = rep };
  } else {
    let samplingProb = mtxElemMul(tree.outMsg, preorderMsg);
    // printLn("Node");
    // printMtx(samplingProb);
    let initRep = suggestRepAligned(samplingProb, 1, nhosts);
    let rep = suggestRepRS(samplingProb, nhosts, initRep);
    // Sample the incident branch
    // printLn(real2string(parentAge));
    // printLn(real2string(tree.age));
    let unorderedBranch = sampleBranch(parentRep, rep, parentAge, tree.age, 1, nhosts, uParams);
    // Since we sampled a repertoire at the node we should propagate this information to the child nodes
    let newMsg = mtxCreate(nhosts, 3, observationMessage2(rep, 1, nhosts));
    let leftMsg = mtxMul(newMsg, tree.leftKernel);
    let rightMsg = mtxMul(newMsg, tree.rightKernel);
    let left = sampleNodeRepertoires(tree.left, nhosts, leftMsg, rep, tree.age, uParams);
    let right = sampleNodeRepertoires(tree.right, nhosts, rightMsg, rep, tree.age, uParams);
    return NodeRepNode {
      age = tree.age, label = tree.label, repertoire = unorderedBranch, left = left, right = right
    };
  }
}




// Compute leaf message from observed interactions
function observationMessage2(obsRepertoire: Int[], i: Int, max: Int): Real[] {
  if i <= max {
    let stateMsg = makeStateMessage2(obsRepertoire[i]);
    // Aligned, so don't use join
    return cons(
      stateMsg[1],
      cons(
        stateMsg[2],
        cons(
          stateMsg[3],
          observationMessage2(obsRepertoire, i + 1, max)
        )
      )
    );
  } else {
    return [];
  }
}

function makeStateMessage2(interaction: Int): Real[] {
  // printLn("Makestatemessage");
  // printLn(int2string(interaction));
  if interaction == 0 {
    return [1., 0., 0.];
  } else {
    if interaction == 1 {
      return [0., 1., 0.];
    } else {
      if interaction == 2 {
        return [0., 0., 1.];
      } else {
        return [1., 1., 1.];
      }
    }
  } 
}



function mtx3ToSeq(mtx: Matrix[Real], i: Int ) {
  let p1 = mtxGet(i, 1, mtx); 
  let p2 = mtxGet(i, 2, mtx); 
  let p3 = mtxGet(i, 3, mtx); 
  let s = p1 + p2 + p3;
  return [p1 / s, p2 / s, p3 / s];
}

function is2(x: Int): Bool {
  return eqi(x, 2);
}


function ones(nOnes: Int) {
  if nOnes > 0 {
    return cons(
      1.,
      ones(subi(nOnes, 1))
    );
  } else {
    return [];
  }
}

function rateMatrixToEmbeddedMarkovChain(qMatrix: Matrix[Real]): EmbeddedMarkovChainMatrix {
  /*
   * Construct a sequence representations of the qMatrix which we can later use for sampling
   * from the embedded Markov Chain
   */ 
  return EmbeddedMarkovChainMatrix {
    totalRates = [mtxGet(1, 1, qMatrix), mtxGet(2, 2, qMatrix), mtxGet(3, 3, qMatrix)],
    transitionProbs = [
      [0.0, mtxGet(1, 2, qMatrix) / mtxGet(1, 1, qMatrix), mtxGet(1, 3, qMatrix) / mtxGet(1, 1, qMatrix)],
      [mtxGet(2, 1, qMatrix) / mtxGet(2, 2, qMatrix), 0.0, mtxGet(2, 3, qMatrix) / mtxGet(2, 2, qMatrix)],
      [mtxGet(3, 1, qMatrix) / mtxGet(3, 3, qMatrix), mtxGet(3, 2, qMatrix) / mtxGet(3, 3, qMatrix), 0.0]
    ]
  };
}

function printSeq(s: Real[], i: Int, max: Int): () {
  if i <= max {
    print(real2string(s[i]));
    print(", ");
    printSeq(s, i + 1, max);
  } else{
    printLn("");
  }
}
function printSeqInt(s: Int[], i: Int, max: Int): () {
  if i <= max {
    print(int2string(s[i]));
    print(", ");
    printSeqInt(s, i + 1, max);
  } else{
    printLn("");
  }
}

